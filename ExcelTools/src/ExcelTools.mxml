<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:ns1="*"
					   width="1024" height="600" preinitialize="init();">	
	<fx:Script>
		<![CDATA[			
			import as3.Config;
			import as3.ISubModule;
			
			import mx.collections.XMLListCollection;
			import mx.controls.Alert;
			import mx.events.MenuEvent;
			
			[Bindable]			
			private var menuCollectxml:XMLListCollection;
			
			private var editorCache_:Dictionary = new Dictionary();

			//加载关联的库
			BuildData;

			private function init():void {
				var menubarXml:XMLList = menuDecl.menuitem;
				menuCollectxml = new XMLListCollection(menubarXml);
				
				var so:SharedObject = SharedObject.getLocal("exceltools_work_space");
				if(so.data.path!=undefined){
					Config.RESOURCE_PATH = so.data.path;
				}else{
					var e:MenuEvent = new MenuEvent("itemClick");
					e.item = new XML("<menuitem label=\"切换工作空间\" data=\"set-path\" toggled=\"true\"/>");
					menuItemClicked(e);
				}
			}
			
			private function menuItemClicked(event:MenuEvent):void {
				var clazz:String = event.item.@clazz;
				if (clazz) {
					content.removeAllChildren();
					var cur_submod:ISubModule = editorCache_[clazz]; 
					if (!cur_submod) {
						var clz:Class = flash.utils.getDefinitionByName(clazz) as Class;
						cur_submod = new clz() as ISubModule;
						editorCache_[clazz] = cur_submod;
						content.addChild(cur_submod as DisplayObject);
						cur_submod.init();
						cur_submod.start();
					} else {
						content.addChild(cur_submod as DisplayObject);
						cur_submod.start();
					}
				}
				
				var data:String = event.item.@data;
				if (data == "set-path") {
					var path:File = new File();
					path.browseForDirectory("切换工作空间：");
					path.addEventListener(Event.SELECT, selectWorkingPath);
				}
			}
			
			private function selectWorkingPath(event:Event):void {
				Config.RESOURCE_PATH = (event.target as File).nativePath;
				var so:SharedObject = SharedObject.getLocal("exceltools_work_space");
				so.data.path = Config.RESOURCE_PATH;
				Alert.show("已成功将工作空间切换至：\n" + Config.RESOURCE_PATH);
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
		<fx:XML id="menuDecl" source="asset/menu.xml"/>
	</fx:Declarations>
	<mx:MenuBar id="menuBar" width="100%" dataProvider="{menuCollectxml}"
				itemClick="menuItemClicked(event);" labelField="@label">
	</mx:MenuBar>
	<mx:Canvas id="content" x="0" y="21" width="100%" height="100%">
	</mx:Canvas>
</s:WindowedApplication>
